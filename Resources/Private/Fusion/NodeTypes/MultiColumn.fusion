prototype(Jonnitto.MultiColumn:1) < prototype(Neos.Fusion:Tag) {

    attributes.class = ${String.toLowerCase(String.pregReplace(q(node).property('_nodeType.name'), '/[[:^alnum:]]/', '-'))}

    content = Neos.Neos:ContentCollection {
        nodePath = 'content'
    }

    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}

prototype(Jonnitto.MultiColumn:2) < prototype(Jonnitto.MultiColumn:Content)

prototype(Jonnitto.MultiColumn:3) < prototype(Jonnitto.MultiColumn:Content) {
    @context.layoutSM = Neos.Fusion:Case {
        @context {
            sm = ${q(node).property('layoutSM')}
            md = ${q(node).property('layoutMD')}
        }

        dontOverwrite {
            condition = ${sm != null}
            renderer = ${sm}
        }

        bigColFirst {
            condition = ${md == '6-3-3'}
            renderer = '12-6-6'
        }

        bigColLast {
            condition = ${md == '3-3-6'}
            renderer = '6-6-12'
        }

        default {
            condition = true
            renderer = ${sm}
        }
    }
}

prototype(Jonnitto.MultiColumn:4) < prototype(Jonnitto.MultiColumn:Content) {
    prototype(Jonnitto.MultiColumn:ContentCollection).attributes.class {
        @context.hasNoLayout = ${!layoutXS && !layoutSM && !layoutMD && !layoutLG && !layoutXL}
        sm = ${layoutSM ? 'col-sm-' + String.split(layoutSM, '-')[iteration.index] : hasNoLayout ? 'col-sm-6' : false}
        md = ${layoutMD ? 'col-md-' + String.split(layoutMD, '-')[iteration.index] : hasNoLayout ? 'col-md-3' : false}
    }
}
